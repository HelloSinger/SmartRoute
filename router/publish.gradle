apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

Properties config = new Properties()
config.load(project.file('../VERSION.properties').newDataInputStream())
def VERSION = config.getProperty("ROUTER_VERSION")
group = "com.kong.router"
version = VERSION

def siteUrl = 'https://github.com/kongpengcheng/SmartRoute'
def issuesUrl = 'https://github.com/kongpengcheng/SmartRoute.git'
def gitUrl = 'https://github.com/kongpengcheng/SmartRoute.git'

install {
    repositories.mavenInstaller.pom.project {
        packaging 'jar'
        name 'SmartRouter'
        artifactId "router"
        description 'Simple and flexible router library for Android platform.'
        url siteUrl

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
        developers {
            developer {
                id 'chenenyu'
                name 'chenenyu'
                email 'ceychina@gmail.com'
            }
        }
    }
}

task jar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    options.encoding = 'UTF-8'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.getDestinationDir()
    classifier = 'javadoc'
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    Properties properties = new Properties()
    boolean hasLocalFile = false
    if (project.rootProject.file('local.properties').exists()) {
        hasLocalFile = true
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
    }
    user = hasLocalFile ? properties.getProperty("bintray.user") : ""
    key = hasLocalFile ? properties.getProperty("bintray.apikey") : ""

    configurations = ['archives']
    pkg {
        repo = "smartrouter"
        name = "router"
        websiteUrl = 'https://github.com/kongpengcheng/SmartRoute'
        vcsUrl = 'https://github.com/kongpengcheng/SmartRoute.git'
        desc = "Annotation compiler for SmartRouter library."
        labels = ["Android", "SmartRouter", "annotation", "compiler"]
        licenses = ["Apache-2.0"]
        publicDownloadNumbers = true
        publish = true
    }
}
